plugins {
    id "eu.xenit.docker-compose" version "5.3.0"
    id 'org.springframework.boot' version '2.6.6' apply false
    id 'io.spring.dependency-management'  version '1.0.11.RELEASE' apply false
    id "be.vbgn.ci-detect" version "0.5.0"
}

subprojects {
    group = 'eu.xenit.alfresco.client'
    version = '1.0.2'

    boolean isRelease = ci.branch?.startsWith("release")
    if (!isRelease) {
        version += "-SNAPSHOT"
    }

    pluginManager.withPlugin('java-library') {

        sourceCompatibility = '1.8'

        java {
            withSourcesJar()
            withJavadocJar()
        }
    }

    pluginManager.withPlugin('org.springframework.boot') {
        bootJar {
            enabled = false
        }
    }

}

ext {
    lombokVersion = '1.18.24'
    jacksonVersion = '2.13.2'
    junitVersion = '5.8.2'
    assertjVersion = '3.21.0'
    mockitoVersion = '4.4.0'
    commonsIoVersion= '2.11.0'
    dittoVersion = '0.6.0'
}

dockerCompose {
    dockerComposeWorkingDirectory = './docker-compose'

    // For running integration test during local development
    // stopContainers = false

    environment.put 'SERVICE_ALFRESCO_PORT_8080', project.findProperty('service.alfresco.tcp.8080') ?: '8080:8080'
    environment.put 'SERVICE_ALFRESCO_PORT_8443', project.findProperty('service.alfresco.tcp.8443') ?: '8443:8443'
}
